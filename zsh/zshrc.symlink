# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

export ZSH=~/.oh-my-zsh

export ZSH_THEME="muse"
# export ZSH_THEME="agnoster"

plugins=(git)

source $ZSH/oh-my-zsh.sh

alias tmux="TERM=xterm-256color tmux"
#export TERM="xterm-256color"
if [ "$TMUX" = "" ]; then tmux; fi

# OpenFOAM development
alias of21="source ~/OpenFOAM/OpenFOAM-2.1.x/etc/bashrc"

# Set vi-mode
bindkey -v
bindkey "^R" history-incremental-search-backward
