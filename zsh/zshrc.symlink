include () {
    if [[ -a "$1" ]]
    then
        source "$1"
    fi
}
# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
include ~/.localrc

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# oh-my-zsh
export ZSH=~/.oh-my-zsh

# On a local machine
if [[ -z "$SSH_CLIENT" ]]
then
    if [[ "$USER" == "root" ]]
    then
        export ZSH_THEME="darkblood"
    else
        export ZSH_THEME="muse"
    fi
else # On a remote machine
    if [[ "$USER" == "root" ]]
    then
        export ZSH_THEME="mh"
    else
        export ZSH_THEME="juanghurtado"
    fi
fi

plugins=(git git-flow vi-mode web-search nyan lol fabric)
source $ZSH/oh-my-zsh.sh

# TMUX
export TERM="screen-256color"
if [ "$TMUX" = "" ]; then tmux; fi

# OpenFOAM development
alias of21="source ~/OpenFOAM/OpenFOAM-2.1.x/etc/bashrc"

bindkey "^R" history-incremental-search-backward
bindkey -M vicmd v edit-command-line

# The next line updates PATH for the Google Cloud SDK.
include '/home/jeremy/google-cloud-sdk/path.zsh.inc'

# The next line enables bash completion for gcloud.
include '/home/jeremy/google-cloud-sdk/completion.zsh.inc'
